import Statement, StatementKind, ParsedType, ExpressionKind, TypeKind, Expression, DefnArgument from "./parser"
import IdentifierOriginKind from "./infer"

class generateTS(block:Statement[]^)
    public const result:string[]

    result.push(value: 'import { __index_get, __index_set, __slice, panic } from "./runtime"')
    result.push(value: 'import { generateTSImport } from "./tboot"')

    function getInitValue(expr: Expression?, type: ParsedType?): string
        if expr <> nil
            return generateJSExpression(expr)
        elseif type?.kind = intType
            return "0"
        elseif type?.kind = arrayType
            return "[]"
        elseif type?.kind = boolType
            return "false"
        elseif type?.kind = mapType
            return "new " + generateTSType(type: type!) + "()"
        else
            return "null"
        end
    end

    function generateBlock(block: Statement[]^, forClass: bool, atRoot: bool)
        var exportClassifier := ""

        if atRoot
            exportClassifier := "export "
        end
       
        for stmt in block
            if stmt.kind = ConstStatement or stmt.kind = LetStatement
                const initValue := getInitValue(expr: stmt.value, type: stmt.type)
                var declaration := "let"

                if stmt.kind = ConstStatement
                    declaration := "const"
                end

                if forClass and stmt.isPublic
                    result.push(value: "_o." + stmt.identifier + " = " + initValue + ";")
                elseif stmt.type <> nil
                    result.push(value: declaration + " " + stmt.identifier + ": " + generateTSType(type: stmt.type) + " = " + initValue + ";")
                else
                    result.push(value: declaration + " " + stmt.identifier + " = " + initValue + ";")
                end
            elseif stmt.kind = IfStatement
                result.push(value: "if (" + generateJSExpression(expr: stmt.value!) + ") {")
                generateBlock(block: stmt.block, forClass: false, atRoot: false)

                for ei in stmt.elseIf
                    result.push(value: "} else if (" + generateJSExpression(expr: ei.value) + ") {")
                    generateBlock(block: ei.block, forClass: false, atRoot: false)
                end
                if stmt.elseBlock.length > 0
                    result.push(value: "} else {")
                    generateBlock(block: stmt.elseBlock, forClass: false, atRoot: false)
                end
                result.push(value: "}")
            elseif stmt.kind = WhileStatement
                result.push(value: "while (" + generateJSExpression(expr: stmt.value!) + ") {")
                generateBlock(block: stmt.block, forClass: false, atRoot: false)
                result.push(value: "}")
            elseif stmt.kind = RepeatStatement
                result.push(value: "do {")
                generateBlock(block: stmt.block, forClass: false, atRoot: false)
                result.push(value: "} while (!(" + generateJSExpression(expr: stmt.value!) + "))")
            elseif stmt.kind = ForStatement
                result.push(value: "for (const " + stmt.identifier + " of " + generateJSExpression(expr: stmt.value!) + ") {")
                generateBlock(block: stmt.block, forClass: false, atRoot: false)
                result.push(value: "}")
            elseif stmt.kind = ForRangeStatement
                result.push(value: "for (let " + stmt.identifier + " = " + generateJSExpression(expr: stmt.lhs!) + "; " + stmt.identifier + " <= " + generateJSExpression(expr: stmt.value!) + "; " + stmt.identifier + "++) {")
                generateBlock(block: stmt.block, forClass: false, atRoot: false)
                result.push(value: "}")
            elseif stmt.kind = ForRangeExclusiveStatement
                result.push(value: "for (let " + stmt.identifier + " = " + generateJSExpression(expr: stmt.lhs!) + "; " + stmt.identifier + " <= " + generateJSExpression(expr: stmt.value!) + "; " + stmt.identifier + "++) {")
                generateBlock(block: stmt.block, forClass: false, atRoot: false)
                result.push(value: "}")
            elseif stmt.kind = AssignStatement
                if stmt.lhs!.kind = Index
                    if effectiveType(type: stmt.lhs!.left!.type).kind = mapType
                        result.push(value: generateJSExpression(expr: stmt.lhs!.left!) + ".set(" + generateJSExpression(expr: stmt.lhs!.indexes[0]) + ", " + generateJSExpression(expr: stmt.value!) + ");")
                    else
                        result.push(value: generateJSExpression(expr: stmt.lhs!.left!) + "[" + generateJSExpression(expr: stmt.lhs!.indexes[0]) + "] = " + generateJSExpression(expr: stmt.value!) + ";")
                    end
                else
                    result.push(value: generateJSExpression(expr: stmt.lhs!) + " = " + generateJSExpression(expr: stmt.value!) + ";")
                end
            elseif stmt.kind = ClassStatement
                var prefix := ""
                var returnDecl := "class_" + stmt.identifier
                if stmt.async
                    prefix := "async "
                    returnDecl := "Promise<" + returnDecl + ">"
                end
                result.push(value: exportClassifier + prefix + "function " + stmt.identifier + "(" + generateDefnArguments(args: stmt.defnArguments) + "):" + returnDecl + " {")
                result.push(value: "const _o = {} as class_" + stmt.identifier + ";")

                for arg in stmt.defnArguments
                    if arg.isPublic
                        result.push(value: "_o." + arg.identifier + " = " + arg.identifier + ";")
                    end
                end

                generateBlock(block: stmt.block, forClass: true, atRoot: false)

                result.push(value: "return _o;")
                result.push(value: "}")
                generateTSInterface(definition: stmt)
            elseif stmt.kind = FunctionStatement
                var prefix := ""
                var returnDecl := generateTSType(type: stmt.type)
                if stmt.async
                    prefix := "async "
                    returnDecl := "Promise<" + returnDecl + ">"
                end
                result.push(value: exportClassifier + prefix + "function " + stmt.identifier + "(" + generateDefnArguments(args: stmt.defnArguments) + "):" + returnDecl + " {")
    
                generateBlock(block: stmt.block, forClass: false, atRoot: false)
    
                result.push(value: "}")

                if forClass and stmt.isPublic
                    result.push(value: "_o." + stmt.identifier + " = " + stmt.identifier + ";")
                end
            elseif stmt.kind = EnumStatement
                result.push(value: "export enum " + stmt.identifier + " {")

                result.push(value: generateJSEnumValues(stmt))

                result.push(value: "};")
            elseif stmt.kind = ReturnStatement
                result.push(value: "return " + generateJSExpression(expr: stmt.value!) + ";")
            elseif stmt.kind = ImportStatement
                result.push(value: "// import goes here")
                result.push(value: generateTSImport(stmt))
            elseif stmt.kind = ExpressionStatement
                result.push(value: generateJSExpression(expr: stmt.value!) + ";")
            else
                result.push(value: "unknown")
            end
        end
    end

    function generateTSInterface(definition: Statement)
        if definition.kind = ClassStatement
            result.push(value: "export interface class_" + definition.identifier + " {")
    
            for arg in definition.defnArguments
                if arg.isPublic
                    result.push(value: arg.identifier + ":" + generateTSType(type: arg.type) + ";")
                end
            end
    
            for stmt in definition.block
                if stmt.kind = FunctionStatement
                    if stmt.isPublic
                        result.push(value: stmt.identifier + "(" + generateDefnArguments(args: stmt.defnArguments) + "):" + generateTSType(type: stmt.type) + ";")
                    end
                elseif stmt.kind = LetStatement or stmt.kind = ConstStatement
                    if stmt.isPublic
                        result.push(value: stmt.identifier + ":" + generateTSType(type: stmt.type) + ";")
                    end
                end
            end
    
            result.push(value: "}")
        end
    end
    
    generateBlock(block, forClass: false, atRoot: true)
end

function generateJSExpression(expr: Expression^):string
    if expr = nil
        return "*nil"
    end

    if expr.type.kind = unknownType and expr.kind <> NilConstant
        panic(message: "expression not inferred")
    end

    if expr.kind = LessThan
        return generateJSExpression(expr: expr.left!) + " < " + generateJSExpression(expr: expr.right!)
    elseif expr.kind = GreaterThan
        return generateJSExpression(expr: expr.left!) + " > " + generateJSExpression(expr: expr.right!)
    elseif expr.kind = Equals
        return generateJSExpression(expr: expr.left!) + " == " + generateJSExpression(expr: expr.right!)
    elseif expr.kind = NotEquals
        return generateJSExpression(expr: expr.left!) + " != " + generateJSExpression(expr: expr.right!)
    elseif expr.kind = GreaterThanEquals
        return generateJSExpression(expr: expr.left!) + " >= " + generateJSExpression(expr: expr.right!)
    elseif expr.kind = LessThanEquals
        return generateJSExpression(expr: expr.left!) + " <= " + generateJSExpression(expr: expr.right!)
    elseif expr.kind = Add
        return generateJSExpression(expr: expr.left!) + " + " + generateJSExpression(expr: expr.right!)
    elseif expr.kind = Subtract
        return generateJSExpression(expr: expr.left!) + " - " + generateJSExpression(expr: expr.right!)
    elseif expr.kind = Multiply
        return generateJSExpression(expr: expr.left!) + " * " + generateJSExpression(expr: expr.right!)
    elseif expr.kind = Divide
        return generateJSExpression(expr: expr.left!) + " / " + generateJSExpression(expr: expr.right!)
    elseif expr.kind = Or
        return generateJSExpression(expr: expr.left!) + " || " + generateJSExpression(expr: expr.right!)
    elseif expr.kind = And
        return generateJSExpression(expr: expr.left!) + " && " + generateJSExpression(expr: expr.right!)
    elseif expr.kind = Dot
        return generateJSExpression(expr: expr.left!) + "." + expr.value
    elseif expr.kind = OptDot
        return generateJSExpression(expr: expr.left!) + "?." + expr.value
    elseif expr.kind = Not
        return "!" + generateJSExpression(expr: expr.left!)
    elseif expr.kind = Bang
        return generateJSExpression(expr: expr.left!) + "!"
    elseif expr.kind = IntConstant
        return expr.value!
    elseif expr.kind = NilConstant
        return "null"
    elseif expr.kind = BoolConstant
        return expr.value!
    elseif expr.kind = StringConstant
        return expr.value!
    elseif expr.kind = Identifier
        if expr.origin!.kind = Field
            return "_o." +  expr.value!
        else
            return expr.value!
        end
    elseif expr.kind = Invoke
        const functionDecl := expr.left!.type.stmt!
        var prefix := ""
        if functionDecl.async
            prefix := "await "
        end
        return prefix + generateJSExpression(expr: expr.left!) + "(" + generateNamedArguments(type: expr.left!.type, args: expr.indexes, argNames: expr.identifiers) + ")"
    elseif expr.kind = Slice
        return "__slice(" + generateJSExpression(expr: expr.left!) + ", " + generateArguments(args: expr.indexes) + ")"
    elseif expr.kind = Index
        if expr.indexes.length = 0
            return "[]"
        end
        if effectiveType(type: expr.left!.type).kind = stringType
            return generateJSExpression(expr: expr.left!) + ".charCodeAt(" + generateArguments(args: expr.indexes) + ")"
        elseif effectiveType(type: expr.left!.type).kind = mapType
            return generateJSExpression(expr: expr.left!) + ".get(" + generateArguments(args: expr.indexes) + ")"
        elseif effectiveType(type: expr.left!.type).kind = arrayType
            return generateJSExpression(expr: expr.left!) + "[" + generateArguments(args: expr.indexes) + "]"
        else
            return "__index_get(" + generateJSExpression(expr: expr.left!) + ", " + generateArguments(args: expr.indexes) + ")"
        end
    else
        return "*expression*"
    end
end

function effectiveType(type: ParsedType) : ParsedType
    if type.kind = pointerType or type.kind = nullableType
        return effectiveType(type: type.ref!)
    end
    return type
end

function generateTSType(type: ParsedType?): string
    if type = nil
        return "any"
    end

    if type.kind = intType
        return "number"
    elseif type.kind = boolType
        return "boolean"
    elseif type.kind = stringType
        return "string"
    elseif type.kind = arrayType
        return generateTSType(type: type.ref) + "[]"
    elseif type.kind = pointerType
        return generateTSType(type: type.ref)
    elseif type.kind = nullableType
        return generateTSType(type: type.ref) + " | null"
    elseif type.kind = objectType
        if type.stmt?.kind = ClassStatement
            return "class_" + type.identifier!
        elseif type.stmt?.kind = EnumStatement
            return type.identifier!
        end
        return "any"
    elseif type.kind = mapType
        return "Map<" + generateTSType(type: type.mapKeyRef) + "," + generateTSType(type: type.ref) + ">"
    elseif type.kind = functionType
        return "Function"
    elseif type.kind = voidType
        return "void"
    elseif type.kind = enumType
        return type.identifier!
    else
        return "any"
    end
end

function generateDefnArgument(arg: DefnArgument): string
    return arg.identifier + ":" + generateTSType(type: arg.type)
end

function generateDefnArguments(args: DefnArgument[]^): string
    if args.length = 0
        return ""
    end

    var result := generateDefnArgument(arg: args[0])
    var idx := 1
    while idx < args.length
        result := result + "," + generateDefnArgument(arg: args[idx])
        idx := idx + 1
    end
    return result
end

function generateNamedArguments(type: ParsedType, args: Expression[], argNames: string[]): string
    if type.kind <> functionType and type.kind <> classType 
        panic(message: "not a function type")
    end
    const defnArguments := type.stmt!.defnArguments

    if defnArguments.length = 0
        return ""
    end

    function generateClosureParams(args: DefnArgument[]^): string
        if args.length = 0
            return ""
        end

        var result := args[0].identifier
        var idx := 1
        while idx < args.length
            result := result + "," + args[idx].identifier
            idx := idx + 1
        end
        return result
    end

    function generateArg(idx: int): string
        const argType := defnArguments[idx].type
        var aidx := 0
        while aidx < argNames.length
            if argNames[aidx] = defnArguments[idx].identifier
                if argType.kind = functionType
                    return "(" + generateClosureParams(args: argType.stmt!.defnArguments) + ")=>" + generateJSExpression(expr: args[aidx])
                end

                return generateJSExpression(expr: args[aidx])
            end
            aidx := aidx + 1
        end

        if defnArguments[idx].value <> nil
            return generateJSExpression(expr: defnArguments[idx].value!)
        end

        panic(message: "missing argument " + defnArguments[idx].identifier + " in call to " + type.stmt!.identifier!)
        return ""
    end

    var result := generateArg(idx: 0)

    var idx := 1
    while idx < defnArguments.length
        result := result + ", " + generateArg(idx: idx)
        idx := idx + 1
    end

    var aidx := 0
    while aidx < argNames.length
        var found := false
        var didx := 0
        while didx < defnArguments.length
            if argNames[aidx] = defnArguments[didx].identifier
                found := true
            end
            didx := didx + 1
        end
        if not found
            panic(message: "unknown argument " + argNames[aidx] + " in call to " + type.stmt!.identifier!)
        end
        aidx := aidx + 1
    end

    return result
end

function generateArguments(args: Expression[]^): string
    if args.length = 0
        return ""
    end
    var result := generateJSExpression(expr: args[0])
    var idx := 1
    while idx < args.length
        result := result + ", " + generateJSExpression(expr: args[idx])
        idx := idx + 1
    end
    return result
end

function generateJSEnumValues(stmt: Statement^): string
    var result := stmt.identifierList[0]

    var idx := 1
    while idx < stmt.identifierList.length
        result := result + ", " + stmt.identifierList[idx]
        idx := idx + 1
    end

    return result
end

function formatParsedType(type: ParsedType?): string
    if type = nil
        return "no type"
    elseif type.kind = objectType
        if type.stmt = nil
            return "object"
        else
            return "object<" + type.stmt.identifier + ">"
        end
    elseif type.kind = arrayType
        return "array<" + formatParsedType(type: type.ref) + ">"
    elseif type.kind = stringType
        return "string"
    elseif type.kind = intType
        return "int"
    elseif type.kind = boolType
        return "bool"
    elseif type.kind = nullableType
        return "nullable<" + formatParsedType(type: type.ref) + ">"
    else
        return "unknown"
    end
end

